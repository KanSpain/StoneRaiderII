*
* DEL 5
TILF   RMB   1
TXT19  FCC   /** RANDOMIZE! **/
       FCC   0
TXT1A  FCC   /                /
       FCC   0
TXTOUT STD   CURSET
       JMP   STROUT
GAME   NOP
       LDA   #$55
       STA   $71
       LDD   #GAME
       STD   $72
TITLES JSR   PCLS
       JSR   HIRES
       CLR   TILF
       LDX   #4361
       JSR   DRWTTL
       LDX   #TXT5
       LDD   #$0704
       BSR   TXTOUT
       LDD   #$0614
       BSR   TXTOUT
       LDD   #$0A60
       BSR   TXTOUT
       LDD   #$0A68
       BSR   TXTOUT
       LDY   #5
       LDD   #$0970
LOOP1A PSHS  D,Y
       BSR   TXTOUT
       PULS  D,Y
       ADDD  #8
       LEAY  -1,Y
       BNE   LOOP1A
       LDX   #LNGTXT
       STX   BYTES+1
WRONG  LDY   #CODE
TSTKEY LDD   #$00B4
       STD   CURSET
       LDB   #32
       STB   BYTES
       LDX   BYTES+1
LOOP42 LDA   ,X+
       JSR   CHROUT
       DEC   BYTES
       BNE   LOOP42
       LEAX  -31,X
       CMPX  #TXTEND
       BLO   LAB2B
       LDX   #LNGTXT
LAB2B  STX   BYTES+1
       LDX   #$0350
LOOP43 LDA   $FF00
       ANDA  #$01
       BEQ   SGAME
       PSHS  Y
       JSR   $BBE5
       PULS  Y
       TSTA
       BNE   LAB2C
       LEAX  -1,X
       BNE   LOOP43
       BRA   TSTKEY
LAB2C  CMPA  #'R
       BNE   TSTPSW
       TST   TILF
       BNE   RESET
       INC   TILF
       LDX   #TXT19
LAB35  LDD   #$08A4
       JSR   TXTOUT
       BRA   WRONG
RESET  CLR   TILF
       LDX   #TXT1A
       BRA   LAB35
TSTPSW CMPA  ,Y+
       BNE   WRONG
       CMPY  #CODE+5
       LBEQ  CHEAT
       BRA   LOOP43
CODE   FCC   /STONE/
BLIVES FCB   3
BCAVE  FCB   1
BSPEED FCB   20
CCAVE  RMB   1
TXT9   FCC   /CAN YOU ESCAPE/,0
TXTA   FCC   /CAVE NO   ??/,0
CALC   LDX   #STAG1-509
       LDB   CCAVE
LOOP3C LEAX  509,X
       DECB
       BNE   LOOP3C
       RTS
SGAME  JSR   ZEROSC
       LDA   BLIVES
       STA   LIVES
       LDA   BCAVE
       STA   CCAVE
NCAVE  LDS   #$05FF
       TST   TILF
       BEQ   LAB36
       JSR   RND
SUB    CMPA  MAXSTG
       BLO   LAB37
       SUBA  MAXSTG
       BRA   SUB
LAB37  INCA
       STA   CCAVE
LAB36  LDX   #NOT
       STX   JUMPS+26
       JSR   PCLS
       LDD   #$0948
       LDX   #TXT9
       JSR   TXTOUT
       LDD   #$0A58
       JSR   TXTOUT
       LDA   CCAVE
       JSR   KONVRT
       LDX   #5906
       LDY   #BYTES+1
       LDB   #2
       JSR   NUMBER
       JSR   MELOD1
       LDA   CCAVE
       CMPA  #15      ;MAGISK
       BEQ   WIZOUT   ;SLIM
       CMPA  #22      ;MAGISK
       BNE   LAB34    ;VEGG
       LDX   #MAGSLM
       STX   JUMPS+26
       BRA   WIZOUT
LAB34  CMPA  #10      ;BEVEGELIG
       BNE   LAB33    ;MUR
       LDX   #WALLM
       STX   JUMPS+26
WIZOUT LDD   #$0780
       LDX   #TXT18
       JSR   TXTOUT
       LDX   #LIGHT
       LDY   #7695
       JSR   DRWGRP
       JSR   MELOD3
       JMP   START2
LAB33  JSR   CALC
       LEAX  9,X
       LDY   #500
LOOP47 LDA   ,X+
       TFR   A,B
       ANDA  #$0F
       CMPA  #$07
       BEQ   FSLIME
       ANDB  #$F0
       CMPB  #$70
       BEQ   FSLIME
       LEAY  -1,Y
       BNE   LOOP47
       BRA   START2
TXT17  FCC   /WATCH OUT FOR THE/
       FCC   / SLIME!/,0
TXT18  FCC   /THE WIZARD IS OUT/
       FCC   /!/,0
FSLIME LDX   #TXT17
       LDD   #$0480
       JSR   TXTOUT
       JSR   SNDON
       LDA   #$FF
       STA   DECAY
       LDD   #40
       STD   SPEED
       LDA   #10
       STA   BYTES
PLOOP  LDX   #GDATA1+224
       LDY   #7695
       JSR   DRWGRP
       LDU   #SLMSD1
       JSR   PLAY
       LDX   #GDATA2+224
       LDY   #7695
       JSR   DRWGRP
       LDU   #SLMSD2
       JSR   PLAY
       DEC   BYTES
       BNE   PLOOP
       JSR   SNDOFF
START2 CLR   SNDTYP
       LDX   #NSOUND
       STX   SWI1
       LDA   #$7E
       STA   SWI1-1
       JSR   CALC
       JSR   MOVSTG
       LDA   BSPEED
       STA   COUNT
       LDA   #10
       STA   PUFF
       STA   EFLAG
       LDD   #GDATA1
       STD   CHRSET
       LDD   #MANL1
       STD   DIR
       JSR   PCLS
       JSR   PRTTOP
       JSR   PSCORE
       JSR   PPOINT
       JSR   PLIVES
       JSR   PBONUS
       JSR   PDLEFT
GLOOP  JSR   $BD52
PRTMAZ TST   DIRBUF
       BNE   CLRBUF
       LDA   #32
       STA   DIRBUF
       LDD   $015A
       CMPA  #2
       BLO   LAB7
       CMPA  #61
       BHI   LAB7
       CMPB  #2
       BLO   LAB7
       CMPB  #61
       BHI   LAB7
CLRBUF CLR   DIRBUF
LAB7   LDX   #STAGE
       LDD   BASE
       ABX
       LDB   #40
       MUL
       LEAX  D,X
       TFR   X,U
       LDY   #3584
       LDA   #11
LOOPA  LDB   #16
LOOP9  PSHS  D
PRT    LDB   ,U
       ANDB  #$0F
       CMPB  #$06
       BNE   LAB1
PRTMAN LDX   DIR
       LDB   DIRBUF
       ABX
       BRA   GRPOUT
LAB1   LDX   CHRSET
       LDA   #32
       MUL
       LEAX  D,X
GRPOUT PSHS  Y,U
       LDA   #16
LOOP45 LDU   ,X++
       STU   ,Y
       LEAY  32,Y
       DECA
       BNE   LOOP45
       PULS  Y,U
NOPRT  PULS  D
       LEAU  1,U
       LEAY  2,Y
       DECB
       BNE   LOOP9
       LEAY  480,Y
       LEAU  24,U
       DECA
       BNE   LOOPA
       LDX   CHRSET
       LEAX  512,X
       STX   CHRSET
       CMPX  #GDATA4
       BHI   HIGH
       BRA   LAB2D
HIGH   LDX   #GDATA1
       STX   CHRSET
LAB2D  LDA   #1
       CLR   SFLAG1
       STA   SFLAG2
       STA   ALIVE
MOVALL LDX   #STAGE+41
LOOPE  LDB   ,X
       LSLB
       BCS   LAB2E
       LDB   ,X
       ANDB  #$0F
       LSLB
       LDY   #JUMPS
       JSR   (B,Y)
LAB2E  LDA   -81,X
       ANDA  #$7F
       STA   -81,X
       LEAX  1,X
       CMPX  #STAGE+1000
       BLO   LOOPE
       LEAX  -81,X
LOOP46 LDA   ,X
       ANDA  #$7F
       STA   ,X+
       CMPX  #STAGE+960
       BLO   LOOP46
SLMGEM LDA   CCAVE
       CMPA  #15      ;MAGISK
       BEQ   DECBON   ;SLIM
       TST   SFLAG1
       BEQ   DECBON
       TST   SFLAG2
       BEQ   SLMBOL
       CLR   SFLAG1
       LDX   #STAGE+40
LOOP11 LDA   ,X+
       ANDA  #$0F
       CMPA  #$07
       BNE   NESTE
       LDA   #$8B
       STA   -1,X
NESTE  CMPX  #STAGE+960
       BLO   LOOP11
       JSR   GEMFAL
       BRA   DECBON
SLMBOL LDA   SFLAG1
       CMPA  #150
       BLO   DECBON
       LDX   #STAGE+40
LOOP18 LDA   ,X+
       ANDA  #$0F
       CMPA  #$07
       BNE   LAB1E
       LDA   #$8C
       STA   -1,X
LAB1E  CMPX  #STAGE+960
       BLO   LOOP18
       JSR   EXPLOD
DECBON SWI
       LDA   BONUS
       BEQ   TSTEXT
       CMPA  #9
       BHI   LAB19
       JSR   FLASH
LAB19  DEC   COUNT
       BNE   TSTEXT
       LDA   BSPEED
       STA   COUNT
       DEC   BONUS
       JSR   PBONUS
       TST   BONUS
       BNE   TSTEXT
TOUT   JSR   HIRES
       LDX   #STAGE+40
LOOP14 LDA   ,X+
       ANDA  #$0F
       CMPA  #$06
       BNE   LAB1D
       LDA   #$82
       STA   -1,X
LAB1D  CMPX  #STAGE+960
       BLO   LOOP14
       LDX   #3072
       CLRA
       CLRB
LOOP17 STD   ,X++
       CMPX  #3584
       BLO   LOOP17
       LDX   #TXT2
       LDD   #$0904
       JSR   TXTOUT
TSTEXT TST   EFLAG
       BEQ   TSTBUT
       TST   DLEFT
       BNE   TSTBUT
       CLR   EFLAG
       LDX   #STAGE
LOOP13 LDA   ,X+
       ANDA  #$0F
       CMPA  #$0F
       BNE   NESTE2
       LDA   #$8A
       STA   -1,X
NESTE2 CMPX  #STAGE+1000
       BLO   LOOP13
       LDA   $FF23
       ORA   #$08
       STA   $FF23
       CLRA
LOOP37 TFR   A,B
LOOP36 STB   $FF20
       INCB
       BNE   LOOP36
       INCA
       BPL   LOOP37
       LDA   $FF23
       ANDA  #$F7
       STA   $FF23
TSTBUT LDA   $FF00
       ANDA  #$01
       BEQ   NLIVE
       JSR   $BBE5
       LBEQ  GLOOP
       CMPA  #32
       BEQ   PAUSE
       CMPA  #3
       BEQ   NLIVE2
       CMPA  #81
       LBNE  GLOOP
       JMP   TITLES
BLKTOP LDX   #3072
       LDD   #$0000
LOOP23 STD   ,X++
       CMPX  #3584
       BLO   LOOP23
       RTS
TXTB   FCC   /PAUSE - PRESS /
       FCC   /BUTTON/,0
PAUSE  BSR   BLKTOP
       LDD   #$0604
       LDX   #TXTB
       JSR   TXTOUT
LOOP26 LDA   $FF00
       ANDA  #$01
       BNE   LOOP26
       BSR   BLKTOP
       JSR   PRTTOP
       JSR   PSCORE
       JSR   PBONUS
       JSR   PLIVES
       JSR   PDLEFT
       JSR   PPOINT
       JMP   GLOOP
NLIVE  TST   ALIVE
       LBEQ  GLOOP
NLIVE2 JSR   HIRES
       DEC   LIVES
       BEQ   GAMEND
       JMP   START2
TXTC   FCC   /** GAME  OVER **/
       FCB   0
GAMEND JSR   PCLS
       LDD   #$0840
       LDX   #TXTC
       JSR   TXTOUT
       JSR   MELOD2
       LDB   #5
       LDY   #HIGHS+8
LOOP24 STY   BYTES
       LDX   #SCORE
LOOP2B LDA   ,X+
       ADDA  #48
       CMPA  ,Y+
       BHI   ENAME
       BLO   LAB24
       CMPX  #SCORE+6
       BLO   LOOP2B
LAB24  LDY   BYTES
       LEAY  15,Y
       DECB
       BNE   LOOP24
       JMP   TITLES
TXTD   FCC   /WHAT IS YOUR /
       FCC   /NAME?/,0
ENAME  LDD   BYTES
       CMPD  #HIGHS+68
       BEQ   ENAME2
       ADDD  #7
       STD   BYTES+2
       LDX   #HIGHS+75
LOOP2D LDA   -16,X
       STA   ,-X
       CMPX  BYTES+2
       BNE   LOOP2D
ENAME2 LDX   #SCORE
       LDY   BYTES
LOOP21 LDA   ,X+
       ADDA  #48
       STA   ,Y+
       CMPX  #SCORE+6
       BLO   LOOP21
       LEAY  -14,Y
       PSHS  Y
       LDX   #TXTD
       LDD   #$0790
       JSR   TXTOUT
       PULS  Y
       LDD   #$0DA0
       STD   CURSET
       LDB   #6
       STB   BYTES
LOOP28 LDA   $FF00
       ANDA  #$01
       BEQ   LOOP28
       LDA   #65
LOOP22 PSHS  A,Y
       JSR   CHROUT
       DEC   CURSET
       JSR   $BD52
       PULS  A,Y
       LDB   $FF00
       ANDB  #$01
       BNE   LAB20
       STA   ,Y+
       INC   CURSET
       JSR   SON2
       CLRA
LOOP2A PSHS  A
LOOP29 STA   $FF20
       INCA
       BNE   LOOP29
       PULS  A
       INCA
       BNE   LOOP2A
       JSR   SOFF2
       DEC   BYTES
       BNE   LOOP28
       JMP   TITLES
LAB20  LDB   $015A
       CMPB  #1
       BHI   LAB21
       CMPA  #65
       BNE   LAB25
       LDA   #$5E
LAB25  DECA
       BRA   VENT
LAB21  CMPB  #62
       BLO   LOOP22
       CMPA  #$5D
       BNE   LAB26
       LDA   #64
LAB26  INCA
       BRA   VENT
VENT   LDX   #$4000
LOOP2C LEAX  -1,X
       BNE   LOOP2C
       BRA   LOOP22
SON2   LDA   $FF23
       ORA   #$08
       STA   $FF23
       RTS
SOFF2  LDA   $FF23
       ANDA  #$F7
       STA   $FF23
       RTS
CHEAT  JSR   PCLS
       JSR   WULF
       LDX   #TXTE
       LDD   #$0B10
       JSR   TXTOUT
       LDD   #$0B18
       JSR   TXTOUT
       LDD   #$0538
       JSR   TXTOUT
       LDD   #$0550
       JSR   TXTOUT
       LDD   #$0568
       JSR   TXTOUT
       LDD   #$0580
       JSR   TXTOUT
       LDX   #4889
       LDA   BLIVES
       JSR   PRTTAL
       LDX   #5657
       LDA   BCAVE
       JSR   PRTTAL
       LDX   #6425
       LDA   BSPEED
       JSR   PRTTAL
KEY    JSR   $8006
       CMPA  #'Q
       LBEQ  TITLES
       CMPA  #'1
       BEQ   OPT1
       CMPA  #'2
       BEQ   OPT2
       CMPA  #'3
       BNE   KEY
OPT3   LDX   #6425
       LDD   #$1968
       BSR   ENTER
       TSTB
       BEQ   OPT3
       STB   BSPEED
       BRA   BEEP
OPT2   LDX   #5657
       LDD   #$1950
       BSR   ENTER
       TSTB
       BEQ   OPT2
       CMPB  MAXSTG
       BHI   OPT2
       STB   BCAVE
       BRA   BEEP
OPT1   LDX   #4889
       LDD   #$1938
       BSR   ENTER
       TSTB
       BEQ   OPT1
       STB   BLIVES
       BRA   BEEP
ENTER  STD   CURSET
       LDY   #$FFFF
       LDB   #8
LOOP2E STY   ,X
       LEAX  32,X
       DECB
       BNE   LOOP2E
       LDA   #2
       STA   BYTES
       LDX   #0
KEY2   JSR   $BBE5
       BEQ   KEY2
       CMPA  #'0
       BLO   KEY2
       CMPA  #'9
       BHI   KEY2
       SUBA  #48
       LEAX  A,X
       ADDA  #48
       JSR   CHROUT
       DEC   BYTES
       BEQ   BACKF
       TFR   X,D
       LDA   #10
       MUL
       TFR   D,X
       BRA   KEY2
BACKF  TFR   X,D
       RTS
BEEP   JSR   SNDON
       LDA   #100
       STA   BYTES
       LDB   #200
LOOP2F LDA   #63
       JSR   MSOUND
       JSR   PITCH
       CLRA
       JSR   MSOUND
       JSR   PITCH
       DEC   BYTES
       BNE   LOOP2F
       JSR   SNDOFF
       JMP   KEY
PRTTAL JSR   KONVRT
       LDY   #BYTES+1
       LDB   #2
       JMP   NUMBER
 
